// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/11/Pong/PongGame.jack
// (Same as projects/9/Pong/PongGame.jack)
/**
 * Represents a Pong game.
 */
class PongGame {

    static PongGame instance; // A Pong game     
    field Bat bat;            // bat
    field Bat topBat;         // top bat
    field Ball ball;          // ball
    field int wall;           // current wall that the ball is bouncing off of
    field boolean exit;       // true when the game is over
    field int score;          // current score
    field int lastWall;       // last wall that the ball bounced off of
    field int loser;          // 1 = player1 lost, 2 = player2 lost

    // The current width of the bat
    field int batWidth;

    /** Constructs a new Pong game. */
    constructor PongGame new() {
	    do Screen.clearScreen();
        let batWidth = 50;  // initial bat size
        let bat = Bat.new(230, 229, batWidth, 7);
        let topBat = Bat.new(230, 0, batWidth, 7);
        do topBat.setDirection(0);
        let ball = Ball.new(253, 222, 0, 511, 0, 229);
        do ball.setDestination(400,0);
        do Screen.drawRectangle(0, 238, 511, 240);
	    do Output.moveCursor(22,0);
	    do Output.printString("Score: 0");
	
        let exit = false;
        let loser = 0;
	    let score = 0;
	    let wall = 0;
	    let lastWall = 0;

        return this;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        do bat.dispose();
        do topBat.dispose();
        do ball.dispose();
        do Memory.deAlloc(this);
        return;
    }

    /** Creates an instance of a Pong game. */
    function void newInstance() {
        let instance = PongGame.new();
        return;
    }
    
    /** Returns this Pong game. */
    function PongGame getInstance() {
        return instance;
    }

    /** Starts the game, and handles inputs from the user that control
     *  the bat's movement direction. */
    method void run() {
        var char key;

        while (~exit) {
            // waits for a key to be pressed.
            while ((key = 0) & (~exit)) {
                let key = Keyboard.keyPressed();
                do bat.move();
                do topBat.move();
                do moveBall();
                do Sys.wait(50);
            }

            if (key = 130) { do bat.setDirection(1); }
	        else {
	            if (key = 132) { do bat.setDirection(2); }
		        else {
                    if (key = 122) { do topBat.setDirection(1); }
                    else {
                        if (key = 120) { do topBat.setDirection(2); }
                        else {
                            if (key = 140) { let exit = true; }
                        }
                    }
		        }
            }

            // Waits for the key to be released.
            while ((~(key = 0)) & (~exit)) {
                let key = Keyboard.keyPressed();
                do bat.move();
                do topBat.move();
                do moveBall();
                do Sys.wait(50);
            }
        }

        if (exit) {
            if (loser = 1) {
                do Output.moveCursor(11, 16);
                do Output.printString("Game Over - Jogador 1 perdeu");
            }
            else {
                do Output.moveCursor(11, 16);
                do Output.printString("Game Over - Jogador 2 perdeu");
            }
        }        return;
    }

    /**
     * Handles ball movement, including bouncing.
     * If the ball bounces off a wall, finds its new direction.
     * If the ball bounces off the bat, increases the score by one
     * and shrinks the bat's size, to make the game more challenging. 
     */
    method void moveBall() {
        var int bouncingDirection, batLeft, batRight, ballLeft, ballRight;
        var boolean missed;

        let wall = ball.move();

        if ((wall > 0) & (~(wall = lastWall))) {
            let lastWall = wall;
            let bouncingDirection = 0;
            let batLeft = bat.getLeft();
            let batRight = bat.getRight();
            let ballLeft = ball.getLeft();
            let ballRight = ball.getRight();

            if (wall = 3) {
                let batLeft = topBat.getLeft();
                let batRight = topBat.getRight();
                let missed = (batLeft > ballRight) | (batRight < ballLeft); // verifica se a bola passou pela raquete (se n達o houve colis達o).
                if (~missed) {
                    if (ballRight < (batLeft + 10)) { let bouncingDirection = -1; }
                    else {
                        if (ballLeft > (batRight - 10)) { let bouncingDirection = 1; }
                    }

                    let batWidth = batWidth - 2;
                    do topBat.setWidth(batWidth);      
                    let score = score + 1;
                    do Output.moveCursor(22,7);
                    do Output.printInt(score);
                }
                else {
                    let exit = true;
                    let loser = 2; // player 2 lost (top was passed)
                }
            }

            if (wall = 4) {
                let missed = (batLeft > ballRight) | (batRight < ballLeft); // verifica se a bola passou pela raquete (ou seja, se n達o houve colis達o).
                if (~missed) {
                    if (ballRight < (batLeft + 10)) { let bouncingDirection = -1; }
                    else {
                        if (ballLeft > (batRight - 10)) { let bouncingDirection = 1; }
                    }

                    let batWidth = batWidth - 2;
                    do bat.setWidth(batWidth);      
                    let score = score + 1;
                    do Output.moveCursor(22,7);
                    do Output.printInt(score);
                }
                else {
                    let exit = true;
                    let loser = 1; // player 1 lost (bottom was passed)
                }
            }

            if (~exit) {
                do ball.bounce(bouncingDirection);
            }
        }
        return;
    }
}